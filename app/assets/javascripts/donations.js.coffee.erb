# Place all the behaviors and hooks related to the matching controller here.
# All this logic will automatically be available in application.js.
# You can use CoffeeScript in this file: http://coffeescript.org/

PENNY_COST_PER_STUDENT = 300;
IMPACT_DURATION_RECURRING = 'every month'
IMPACT_DURATION_ONE_TIME = 'for a month'

@['donations#new'] = (data) ->
  impactCount = $('#donation-impact-count')
  impactDuration = $('#donation-impact-duration')

  form = $('#donation-form')
  contributeBtn = $('#contribute')
  amountInput = $('input[name="amount"]')
  recurringInput = $('input[name="recurring"]')

  amount = amountInput.filter(':checked').val()
  recurring = recurringInput.filter(':checked').val()
  stripeHandler = StripeCheckout.configure(
    key: '<%= Figaro.env.STRIPE_PUBLISHABLE_KEY %>'
    image: '<%= asset_url("logo.png") %>'
    locale: 'auto'
    panelLabel: 'Contribute {{amount}}'
    token: (token) ->
      # Add the Stripe token and email to the form, then submit it
      tokenInput = $('<input type="hidden" name="stripe_token" />').val(token.id)
      emailInput = $('<input type="hidden" name="stripe_email" />').val(token.email)

      form.append(tokenInput).append(emailInput).submit()
  )

  # Adjust displayed impact count based on donation amount
  amountInput.change ->
    amount = parseInt this.value
    count = Math.floor(amount / PENNY_COST_PER_STUDENT)
    impactCount.text constructImpactCount(count)

  # Adjust displayed impact duration based on whether the donation is recurring
  recurringInput.change ->
    # Convert the selected value to a boolean and store it in recurring
    recurring = this.value == 'true'

    if recurring
      impactDuration.text IMPACT_DURATION_RECURRING
    else
      impactDuration.text IMPACT_DURATION_ONE_TIME

  # Open Stripe Checkout when the contribute button is clicked
  contributeBtn.on 'click', (e) ->
    stripeHandler.open
      name: 'Hack Club',
      description: constructStripeCheckoutDescription recurring
      amount: amount
    e.preventDefault()

  # Close Stripe Checkout on page navigation
  $(window).on 'popstate', ->
    stripeHandler.close()

constructImpactCount = (count) =>
  if count == 1
    count + ' student'
  else
    count + ' students'

constructStripeCheckoutDescription = (recurring) =>
  suffix = 'Hack Club contribution'

  if recurring
    'Monthly ' + suffix
  else
    'One time ' + suffix
